plugins {
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'java-library'
    id 'org.owasp.dependencycheck' version '11.1.0'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode") ? project.releaseMode.toBoolean() : false
    publishRepo = "https://maven.pkg.github.com/reportportal/commons"
    dependencyRepos = ["commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
//apply from: scriptsUrl + '/build-quality.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE'
        mavenBom('com.epam.reportportal:commons-bom:5.13.0')
    }
}

dependencies {
    if (releaseMode) {
        api 'com.epam.reportportal:commons-reporting:5.13.0'
    } else {
        api 'com.github.reportportal:commons-reporting:develop-SNAPSHOT'
    }
    api 'org.springframework:spring-webmvc'
    api 'org.springframework.security:spring-security-core'
    api 'org.apache.tika:tika-core'

    implementation "org.springdoc:springdoc-openapi-data-rest:${sprindocAnnotationsVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api "javax.validation:validation-api:${validationApiVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    implementation 'javax.servlet:javax.servlet-api'

    implementation 'org.freemarker:freemarker'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'net.coobird:thumbnailator:0.4.11'
    implementation 'com.google.guava:guava'
    implementation 'javax.inject:javax.inject:1'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoJunitJupiter}"
    testImplementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
    testImplementation "javax.el:javax.el-api:${elApiVersion}"
    testImplementation "org.glassfish:javax.el:${elApiVersion}"
}

wrapper {
    gradleVersion = '8.10'
}

tasks.preTagCommit.enabled = false
tasks.updateVersion.enabled = false
tasks.commitNewVersion.enabled = false
