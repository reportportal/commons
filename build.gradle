plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
    id "org.owasp.dependencycheck" version "5.3.1"
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    publishRepo = "https://maven.pkg.github.com/reportportal/commons"
    dependencyRepos = ["commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'github-release-action')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
apply from: scriptsUrl + '/build-quality.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}
dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:efc4947e')
    }
}

dependencies {
    compile 'org.freemarker:freemarker'
    compile 'ch.qos.logback:logback-classic'
    compile 'org.apache.tika:tika-core'
    compile 'net.coobird:thumbnailator:0.4.11'
    implementation 'com.google.guava:guava'
    implementation 'javax.inject:javax.inject:1'

    testImplementation 'junit:junit'
}

wrapper {
    gradleVersion = '6.0'
}
